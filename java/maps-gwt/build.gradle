buildscript {
    repositories {
        jcenter()
        // Alternatively use:
        // maven {
        //    url 'http://dl.bintray.com/steffenschaefer/maven'
        // }
    }
    dependencies {
        classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.6'
    }
}

apply plugin: 'war'
apply plugin: 'gwt'

gwt {
    gwtVersion='2.7.0'

    modules 'org.openmetromaps.maps.gwt.Test'

    compiler {
        strict = true;
        enableClosureCompiler = true;
        disableClassMetadata = true;
        disableCastChecking = true;
    }
}

dependencies {
    compile project(':openmetromaps-maps-model')
}

task depfiles {
    doLast {
        project.configurations.compile.each { println it.name }
    }
}

task depcoords {
    doLast {
        project.configurations.compile.resolvedConfiguration.resolvedArtifacts.each {
            ComponentIdentifier ci = it.artifactId.componentIdentifier
            if (ci instanceof ModuleComponentIdentifier) {
                ModuleComponentIdentifier mci = ci
                println String.format("%s:%s:%s", mci.group, mci.module, mci.version)
            }
        }
    }
}
