task wrapper(type: Wrapper) {
    gradleVersion = 4.3
}

allprojects {
    group = pGroup
    version = pVersion
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'

    repositories {
        maven {
            url 'http://mvn.topobyte.de'
        }
        maven {
            url 'http://mvn.slimjars.com'
        }
        mavenCentral()
    }

    task allDeps(type: DependencyReportTask) {}
}

subprojects {
    task showIntermoduleDependencies {
        doLast {
            println "module '$project.name'"
            project.configurations.compile.resolvedConfiguration.resolvedArtifacts.each {
                if (it.id instanceof org.gradle.internal.component.local.model.PublishArtifactLocalArtifactMetadata) {
                    def component = it.id.componentIdentifier
                    if (component instanceof org.gradle.internal.component.local.model.DefaultProjectComponentIdentifier) {
                        def path = component.projectPath
                        def name = path.substring(path.lastIndexOf(':') + 1)
                        println "     → '$name'"
                    }
                }
            }
        }
    }
}

ext.libraries = [
        project(':openmetromaps-osm-model'),
        project(':openmetromaps-osm'),
        project(':openmetromaps-maps-model'),
        project(':openmetromaps-maps-awt')
]

task buildUploadArtifactList {
    doLast {
        project.ext.libraries.each {
            println "lib: $it"
            it.configurations.compile.resolvedConfiguration.resolvedArtifacts.each {
                if (it.id instanceof org.gradle.internal.component.local.model.PublishArtifactLocalArtifactMetadata) {
                    def component = it.id.componentIdentifier
                    if (component instanceof org.gradle.internal.component.local.model.DefaultProjectComponentIdentifier) {
                        def path = component.projectPath
                        def name = path.substring(path.lastIndexOf(':') + 1)
                        println "     → '$name'"
                    }
                }
            }
        }
    }
}

configure(libraries) {

    apply plugin: 'maven'

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    if (project.hasProperty("topobyte")) {
        apply from: 'file:///' + System.getProperty("user.home") \
            + '/.gradle/topobyte.gradle'
    }

    artifacts {
        archives sourcesJar
    }

    def global = new File(rootProject.projectDir, 'info.pom');
    def local = new File(project.projectDir, 'info.pom');

    def pomInfo = new XmlSlurper().parse(global);
    def projName = pomInfo.name
    def projDesc = pomInfo.description
    if (local.exists()) {
        def pomInfoL = new XmlSlurper().parse(local);
        projName = pomInfoL.name
        projDesc= pomInfoL.description
    }

    afterEvaluate { project ->
        uploadArchives {
            repositories {
                mavenDeployer {
                    pom.project {
                        name projName
                        description projDesc
                        url pomInfo.url
                        licenses {
                            license {
                                name pomInfo.licenseName
                                url pomInfo.licenseUrl
                                distribution pomInfo.licenseDistribution
                            }
                        }
                    }
                }
            }
        }
    }
}
