task wrapper(type: Wrapper) {
    gradleVersion = 4.3
}

allprojects {
    group = pGroup
    version = pVersion
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'

    repositories {
        maven {
            url 'http://mvn.topobyte.de'
        }
        maven {
            url 'http://mvn.slimjars.com'
        }
        mavenCentral()
    }

    task allDeps(type: DependencyReportTask) {}
}

subprojects {
    task showInterModuleDependencies {
        doLast {
            println "module '$project.name'"
            project.configurations.compile.resolvedConfiguration.resolvedArtifacts.each {
                if (it.id instanceof org.gradle.internal.component.local.model.PublishArtifactLocalArtifactMetadata) {
                    def component = it.id.componentIdentifier
                    if (component instanceof org.gradle.internal.component.local.model.DefaultProjectComponentIdentifier) {
                        def path = component.projectPath
                        def name = path.substring(path.lastIndexOf(':') + 1)
                        println "     → '$name'"
                    }
                }
            }
        }
    }
}

ext.libraries = [
        project(':openmetromaps-osm-model'),
        project(':openmetromaps-osm'),
        project(':openmetromaps-maps-model'),
        project(':openmetromaps-maps-model-xml-desktop'),
        project(':openmetromaps-maps-awt'),
        project(':openmetromaps-change-model'),
        project(':openmetromaps-change-model-csv-export'),
        project(':openmetromaps-raw-change-model-xml-desktop'),
]

ext.alllibraries = [
        project(':openmetromaps-osm-model'),
        project(':openmetromaps-osm'),
        project(':openmetromaps-maps-model'),
        project(':openmetromaps-maps-model-util'),
        project(':openmetromaps-maps-awt'),
        project(':openmetromaps-maps-core'),
        project(':openmetromaps-lightgeom'),
        project(':openmetromaps-line-network-util'),
        project(':openmetromaps-misc-util'),
        project(':openmetromaps-swing-misc'),
        project(':openmetromaps-change-model'),
        project(':openmetromaps-change-model-csv-export'),
        project(':openmetromaps-maps-model-xml'),
        project(':openmetromaps-maps-model-xml-desktop'),
        project(':openmetromaps-raw-change-model'),
        project(':openmetromaps-raw-change-model-xml'),
        project(':openmetromaps-raw-change-model-xml-desktop'),
        project(':openmetromaps-xml-dom-abstraction'),
        project(':openmetromaps-xml-dom-abstraction-desktop'),
]

task checkUploadArtifactList {
    doLast {
        // build lookup from module name to module
        def map = [:]
        subprojects.each {
            map.put(it.name, it)
        }

        // build set of modules by looking at dependencies
        def set = new HashSet()
        def queue = new HashSet()

        set.addAll(project.ext.libraries)
        queue.addAll(project.ext.libraries)

        // iterate module queue
        while(!queue.empty) {
            it = queue.iterator().next()
            queue.remove(it)
            println "lib: $it"
            set.add(it)
            it.configurations.compile.resolvedConfiguration.resolvedArtifacts.each {
                if (it.id instanceof org.gradle.internal.component.local.model.PublishArtifactLocalArtifactMetadata) {
                    def component = it.id.componentIdentifier
                    if (component instanceof org.gradle.internal.component.local.model.DefaultProjectComponentIdentifier) {
                        def path = component.projectPath
                        def name = path.substring(path.lastIndexOf(':') + 1)
                        println "     → '$name'"
                        def dep = map.get(name)
                        if (!set.contains(dep)) {
                            set.add(dep)
                            queue.add(dep)
                        }
                    }
                }
            }
        }

        // convert set of modules to sorted list
        def list = []
        list.addAll(set)
        Collections.sort(list)

        // check list of modules against configured list of libaries
        def someMissing = false
        list.each {
            if (!project.ext.alllibraries.contains(it)) {
                println "missing: $it.name"
                someMissing = true
            }
        }

        if (someMissing) {
            println "Add the following lines to your config:"
            list.each {
                if (!project.ext.alllibraries.contains(it)) {
                    println "project(':$it.name'),"
                }
            }
        }
    }
}

configure(alllibraries) {

    apply plugin: 'maven'

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    if (project.hasProperty("topobyte")) {
        apply from: 'file:///' + System.getProperty("user.home") \
            + '/.gradle/topobyte.gradle'
    }

    artifacts {
        archives sourcesJar
    }

    def global = new File(rootProject.projectDir, 'info.pom');
    def local = new File(project.projectDir, 'info.pom');

    def pomInfo = new XmlSlurper().parse(global);
    def projName = pomInfo.name
    def projDesc = pomInfo.description
    if (local.exists()) {
        def pomInfoL = new XmlSlurper().parse(local);
        projName = pomInfoL.name
        projDesc= pomInfoL.description
    }

    afterEvaluate { project ->
        uploadArchives {
            repositories {
                mavenDeployer {
                    pom.project {
                        name projName
                        description projDesc
                        url pomInfo.url
                        licenses {
                            license {
                                name pomInfo.licenseName
                                url pomInfo.licenseUrl
                                distribution pomInfo.licenseDistribution
                            }
                        }
                    }
                }
            }
        }
    }
}
